load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_cc//cc:defs.bzl", "cc_library", "cc_binary")

platform(
    name = "x64_windows-mingw64-gcc",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:windows",
        "@bazel_tools//tools/cpp:mingw",
    ],
)

cc_library(
    name = "custom",
    hdrs = ["custom.h"],
    visibility = ["//visibility:public"]
)

cc_library(
    name = "gettext",
    hdrs = ["gettext.h"],
    visibility = ["//visibility:public"]
)


copy_file(
    name = "config_h",
    src = select({
        "@platforms//os:windows": "//pc:config.h",
        "//conditions:default": "//posix:config.h",
    }),
    out = "config/config.h",
)

cc_library(
    name = "config",
    hdrs = [":config_h"],
    deps = select({
        "@platforms//os:windows": ["//pc"],
        "//conditions:default": ["//posix"],
    }),
    defines = [
        "GAWK",
        "HAVE_CONFIG_H"
    ],
    visibility = ["//visibility:public"]
)   

cc_library(
    name = "awk",
    hdrs = glob(["*.h"]) + ["//missing_d"],
    srcs = glob(["*.c"]),
    includes = ["./support"],
    deps = [
        ":config",
        "//support",
    ],
    linkopts = select({
        "@platforms//os:windows": [
            "-lws2_32",
            # TODO: why there is multiple definitions?
            "-Wl,-allow-multiple-definition"
        ],
        "//conditions:default": []
    })
)

cc_binary(
    name = "gawk",
    srcs = ["main.c"],
    deps = [":awk"],
    visibility = ["//visibility:public"]
)